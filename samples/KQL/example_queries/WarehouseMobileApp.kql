// Telemetry from the SCM Warehouse mobile app

// Warehouse.LocationDirective.FindLocation
customEvents 
| where timestamp >= ago(7d) // adjust as needed
| where customDimensions.eventId == 'WHS000001'
| project timestamp
, activityGraph = customDimensions.activityGraph
, activityId = customDimensions.activityId
, aadTenantId = customDimensions.aadTenantId
, environmentId = customDimensions.environmentId
, eventStatus = customDimensions.eventStatus
, itemId = customDimensions.itemId
, itemRecordId = customDimensions.itemRecordId
, loadId = customDimensions.loadId
, orderNumber = customDimensions.orderNumber
, requestedInventoryQuantity = customDimensions.requestedInventoryQuantity
, shipmentId = customDimensions.shipmentId
, waveId = customDimensions.waveId
, waveProcessingId = customDimensions.waveProcessingId
, workCreationNumber = customDimensions.workCreationNumber
, workOrderType = customDimensions.workOrderType
, workType = customDimensions.workType


// Warehouse.WaveProcessing.PostWave
customEvents 
| where timestamp >= ago(7d) // adjust as needed
| where customDimensions.eventId == 'WHS000002'
| project timestamp
, activityGraph = customDimensions.activityGraph
, activityId = customDimensions.activityId
, aadTenantId = customDimensions.aadTenantId
, environmentId = customDimensions.environmentId
, eventStatus = customDimensions.eventStatus
, waveId = customDimensions.waveId
, waveProcessingId = customDimensions.waveProcessingId


// Warehouse.MobileApp.NextPageRequest
customEvents 
| where timestamp >= ago(7d) // adjust as needed
| where customDimensions.eventId == 'WHS000003'
| project timestamp
, activityGraph = customDimensions.activityGraph
, activityId = customDimensions.activityId
, aadTenantId = customDimensions.aadTenantId
, environmentId = customDimensions.environmentId
, eventStatus = customDimensions.eventStatus
, mobileDeviceId = customDimensions.mobileDeviceId
, mobileDeviceRequestActivityId = customDimensions.mobileDeviceRequestActivityId
, clientCity = client_City
, clientStateOrProvince = client_StateOrProvince
, clientCountryOrRegion = client_CountryOrRegion


// Warehouse.MobileApp.ImageRequest
customEvents 
| where timestamp >= ago(7d) // adjust as needed
| where customDimensions.eventId == 'WHS000004'
| project timestamp
, activityGraph = customDimensions.activityGraph
, activityId = customDimensions.activityId
, aadTenantId = customDimensions.aadTenantId
, environmentId = customDimensions.environmentId
, eventStatus = customDimensions.eventStatus // this can be "start" or "stop"
, mobileDeviceRequestImageDetails = customDimensions.mobileDeviceRequestImageDetails
, mobileDeviceRequestImageIsThumbnail = customDimensions.mobileDeviceRequestImageIsThumbnail

// Warehouse.MobileApp.ImageRequest durations
let _lookback = 7d; // adjust as needed
let image_starts = 
customEvents 
| where timestamp >= ago(_lookback) 
| where customDimensions.eventId == 'WHS000004'
| where customDimensions.eventStatus == 'start'
| project timestamp_start = timestamp
, activityId = tostring(customDimensions.activityId)
, aadTenantId = customDimensions.aadTenantId
, environmentId = customDimensions.environmentId
, mobileDeviceRequestImageDetails = customDimensions.mobileDeviceRequestImageDetails
, mobileDeviceRequestImageIsThumbnail = customDimensions.mobileDeviceRequestImageIsThumbnail
;
let image_ends = 
customEvents 
| where timestamp >= ago(_lookback)
| where customDimensions.eventId == 'WHS000004'
| where customDimensions.eventStatus == 'stop'
| project timestamp_end = timestamp
, activityId = tostring(customDimensions.activityId)
;
image_starts
| join kind=leftouter image_ends on $left.activityId == $right.activityId
| extend duration_ms = datetime_diff('millisecond', timestamp_end, timestamp_start)
| project-rename timestamp = timestamp_start
| project-away activityId1, timestamp_end




// Warehouse.WorkCreation.Log
customEvents 
| where timestamp >= ago(7d) // adjust as needed
| where customDimensions.eventId == 'WHS000005'
| project timestamp
, activityGraph = customDimensions.activityGraph
, activityId = customDimensions.activityId
, aadTenantId = customDimensions.aadTenantId
, environmentId = customDimensions.environmentId
, eventStatus = customDimensions.eventStatus
, waveId = customDimensions.waveId
, waveProcessingId = customDimensions.waveProcessingId
, workCreationLogFailed = customDimensions.workCreationLogFailed // If == 1, then this indicates a failure
, workCreationLogMessage = customDimensions.workCreationLogMessage
, workCreationLogTransactionTime = customDimensions.workCreationLogTransactionTime


// Warehouse.MobileApp.ClientRoundTrip
customEvents 
| where timestamp >= ago(1d) // adjust as needed
| where customDimensions.eventId == 'WHS000006'
| project timestamp
, backendProcessingTime = customDimensions.backendProcessingTime
, batteryLevel = customDimensions.batteryLevel
, batterySession = customDimensions.batterySession
, batteryState = customDimensions.batteryState
, deviceId = customDimensions.deviceId
, isEnergySaverTurnedOn = customDimensions.isEnergySaverTurnedOn
, powerSource = customDimensions.powerSource
, renderingDurationInMilliseconds = customDimensions.renderingDurationInMilliseconds
, roundTripLatencyDurationInMilliseconds = customDimensions.roundTripLatencyDurationInMilliseconds
, clientCity = client_City
, clientStateOrProvince = client_StateOrProvince
, clientCountryOrRegion = client_CountryOrRegion